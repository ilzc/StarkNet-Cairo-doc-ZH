# 与 L1 合约交互

## 背景

一个好的 L2 系统的一个重要特性是能够与它所构建的 L1 系统交互（否则系统实际上是孤立的）。 在本节中，我们将描述 StarkNet 合约如何与以太坊 L1 合约进行交互。

每个 StarkNet 合约都可以向任何 L1 合约发送和接收消息。 通常，建议设计一对合约：一个 L2 合约及其对应的 L1 合约（例如，用 Solidity 编写），并决定两个合约之间的消息协议。

## L2 发送消息到 L1

从 L2 到 L1 的消息工作如下：

1. StarkNet (L2) 合约函数调用库函数 ```send_message_to_l1()``` 以发送消息。 它规定：

* 目的 L1 合约 (to)。

* 要发送的数据（payload）。

StarkNet OS 添加了 ```from``` 地址，即发送消息的合约的 L2 地址。

2. 一旦包含 L2 交易的状态更新在链上被接受，消息就会存储在 L1 StarkNet 核心合约中，等待被消费。

3. to地址指定的L1合约调用StarkNet核心合约的 ```consumeMessageFromL2()```。

注意：由于任何 L2 合约都可以向任何 L1 合约发送消息，因此建议 L1 合约在处理交易之前检查发件人地址。

下面我们将展示如何使用这种机制来实现提款交易。

## L1 发送消息到 L2

1. L1 合约调用（在 L1 上）StarkNet 核心合约的 send_message() 函数，带有消息和费用。核心合约存储消息和费用。在这种情况下，消息包含一个附加字段 - 选择器，它决定在相应的 L2 合约中调用什么函数。

2. 假设费用足够，StarkNet Sequencer 会自动消费该消息并调用由“to”地址指定的合约的请求 L2 函数。

这个方式很有用，例如，对于存款交易。

请注意，虽然诚实的 Sequencer 会自动使用 L1 -> L2 消息，但协议并未强制执行（因此 Sequencer 可能会选择跳过消息）。在设计两个合约之间的消息协议时应该考虑到这一点。

重要提示：StarkNet Alpha 系统仍在开发中，因此系统状态会不时重置，所有合约都将被删除。这意味着你不应该将有价值的资产转移到 StarkNet 系统，除非你有办法在移除 L2 合约的情况下撤回它们。

## 一个 token 桥的示例

在本节中，我们将构建一个简单的代币桥——用户将能够存入 L1 代币，他们的 L2 余额将会增加。 然后，他们将能够提取一些代币，这将减少他们的 L2 余额并增加他们的 L1 余额。

### 一些准备工作

从 ```%lang``` 指令、一些导入和一些常量开始：

```
%lang starknet

from starkware.cairo.common.alloc import alloc
from starkware.cairo.common.cairo_builtins import HashBuiltin
from starkware.cairo.common.math import assert_nn
from starkware.starknet.common.messages import send_message_to_l1

const L1_CONTRACT_ADDRESS = (
    0x8359E4B0152ed5A731162D3c7B0D8D56edB165A0);
const MESSAGE_WITHDRAW = 0;
```

请注意，在实际应用程序中，您可能希望将 L1 合约的地址维护为存储变量，而不是常量。

然后，定义保存余额的存储变量以及 getter ```@view``` 函数：

```
// 一个L1 用户的余额
@storage_var
func balance(user: felt) -> (res: felt) {
}

@view
func get_balance{
    syscall_ptr: felt*,
    pedersen_ptr: HashBuiltin*,
    range_check_ptr,
}(user: felt) -> (balance: felt) {
    let (res) = balance.read(user=user);
    return (balance=res);
}
```

就这样我们将有一些“资金”可以使用，定义一个可以铸造新代币的函数（在实际应用程序中，您可能不想要一个让用户有效“打印”钱的函数。此外，您将检查金额是否为非负数）：

```
@external
func increase_balance{
    syscall_ptr: felt*,
    pedersen_ptr: HashBuiltin*,
    range_check_ptr,
}(user: felt, amount: felt) {
    let (res) = balance.read(user=user);
    balance.write(user, res + amount);
    return ();
}
```

### 发送消息到 L1

向 L1 发送消息对于提款可能很有用：请求提款的用户调用提款 (L2) 交易。 交易减少他们的 L2 余额并向 L1 合约发送消息，指示用户的 L1 余额应增加提取的金额。 L1 对应方应该允许用户消费消息并在这样做时增加他们在 L1 上的余额。

```
@external
func withdraw{
    syscall_ptr: felt*,
    pedersen_ptr: HashBuiltin*,
    range_check_ptr,
}(user: felt, amount: felt) {
    // 确认 amount 是非负数
    assert_nn(amount);

    let (res) = balance.read(user=user);
    tempvar new_balance = res - amount;

    // 确认新余额 > 0
    assert_nn(new_balance);

    // 更新余额
    balance.write(user, new_balance);

    // 发送 提款 消息
    let (message_payload: felt*) = alloc();
    assert message_payload[0] = MESSAGE_WITHDRAW;
    assert message_payload[1] = user;
    assert message_payload[2] = amount;
    send_message_to_l1(
        to_address=L1_CONTRACT_ADDRESS,
        payload_size=3,
        payload=message_payload,
    );

    return ();
}
```

请注意，添加了一个新的隐式参数——系统调用指针 (syscall_ptr)。这个参数允许我们调用 StarkNet 操作系统的一些函数，包括“发送消息”函数。

通过调用 send_message_to_l1() 在withdraw() 结束时发送消息，它获取 L1 合约地址、消息大小和消息本身（作为felt*）。请注意，消息本身是作为指针给出的，因此必须显式传递消息长度。在我们的示例中，消息数据为：MESSAGE_WITHDRAW、用户、金额。我们选择使用第一个元素作为消息类型的指示符（请注意，这里实际上并不需要它，因为我们只有一个消息类型）。

现在让我们看看如何编写 L1 合约对应方。考虑 ```withdraw()``` 函数：它获取用户和金额，消费消息（如果没有在链上收到消息，这部分将失败）并相应地更新用户的余额。正如您将在下面看到的，我们将 L2 合约的地址作为参数传递给函数，这样合约就可以部署一次，任何人都可以使用本教程。然而，通常将 L2 合约的地址作为参数是没有意义的——地址应该为合约的每个实例固定。

### 从 L1 接收信息

为了处理从 L1 合约发送的消息，您应该声明一个 L1 处理程序：

```
@l1_handler
func deposit{
    syscall_ptr: felt*,
    pedersen_ptr: HashBuiltin*,
    range_check_ptr,
}(from_address: felt, user: felt, amount: felt) {
    // 确认消息是从 L1 指定的合约发送过来
    assert from_address = L1_CONTRACT_ADDRESS;

    // 读取当前余额
    let (res) = balance.read(user=user);

    // 计算新的余额
    tempvar new_balance = res + amount;
    balance.write(user, new_balance);

    return ();
}
```

StarkNet OS 调用 L1 处理程序以处理从 L1 合约发送的消息。 一个 StarkNet 合约可以定义一些 L1 处理程序，它们由一个称为选择器的整数值标识。 您可以使用以下 Python 代码根据 L1 处理程序名称计算选择器：

```
from starkware.starknet.compiler.compile import \
    get_selector_from_name

print(get_selector_from_name('deposit'))
```

应该会返回如下信息：
```
352040181584456735608515580760888541466059565068553383579463728554843487745
```

当 L1 合约想要发送消息时，它会调用 StarkNet Core 合约的 sendMessageToL2() 函数，指定 L2 合约地址、要调用的处理程序的选择器和消息负载。 消息费以 ETH 支付，需要随通话一起发送。 有关示例，请参见示例 L1 合约中的存款函数。

### 调用合约

将新的 StarkNet 合约文件保存为 l1l2.cairo。 您可以在此处找到完整的[源文件](https://starknet.io/docs/_static/l1l2.cairo)。

编译并声明合约：

```
starknet-compile l1l2.cairo \
    --output l1l2_compiled.json \
    --abi l1l2_abi.json
starknet declare --contract l1l2_compiled.json
```

部署合约：

```
starknet deploy --class_hash ${L1L2_CLASS_HASH}
```

其中 ```${L1L2_CLASS_HASH}```是 class_hash 的值。 在运行 ```starknet deploy``` 之前，不要忘记将 ```STARKNET_NETWORK``` 环境变量设置为 ```alpha-goerli```。

设置以下环境变量：

```
# 刚才部署的合约地址.
export CONTRACT_ADDRESS="<address of the previous contract>"
```

选择你喜欢的 ```USERID```，它应该是一个 251 位的整数值：
```
export USERID="<favorite 251-bit integer>"
```

调用 ```increase_balance``` 函数：

```
starknet invoke \
    --address ${CONTRACT_ADDRESS} \
    --abi l1l2_abi.json \
    --function increase_balance \
    --inputs \
        ${USERID} \
        3333
```

余额增加后，调用withdraw函数：

```
starknet invoke \
    --address ${CONTRACT_ADDRESS} \
    --abi l1l2_abi.json \
    --function withdraw \
    --inputs \
        ${USERID} \
        1000
```

调用 get_balance 以检查余额是否计算正确（请记住，您必须等到第二笔交易包含在一个块中）：

```
starknet call \
    --address ${CONTRACT_ADDRESS} \
    --abi l1l2_abi.json \
    --function get_balance \
    --inputs \
        ${USERID}
```

你应该得到：
```
2333
```
1,279 / 5,000
翻译结果
等待交易在链上被接受（这可能需要一些时间）——你可以使用 ```starknet tx_status``` 来跟踪交易的进度。然后，调用部署在地址 ```0x8359E4B0152ed5A731162D3c7B0D8D56edB165A0``` 的示例合约的 ```withdraw()``` 函数，并使用以下参数：```CONTRACT_ADDRESS```, ```USERID```, 1000（与之前一样，```CONTRACT_ADDRESS``` 是您部署的 L2 合约的地址）。在```withdraw()```交易之后，用户的L1余额应该是 ```1000``` ，他们的L2余额应该是 ```2333``` 。

在您的withdraw() 交易在链上被接受后，您可以将部分提取的资金存回L2。使用以下参数调用示例合约的 deposit() 函数：```CONTRACT_ADDRESS```, ```USERID```, 600 并以 ETH 支付消息费。对于 Goerli 测试网，您可以使用 0.01 ETH 的消息费。 （请注意，以 ETH 支付的“价值”在 Etherscan 中显示为 ```payableAmount``` 。）StarkNet 处理传入消息并调用 L1 处理程序可能需要一些时间（例如，系统等待一些区块链确认）。但是在那之后，您将能够通过再次调用 ```get_balance``` 的 ```starknet call``` 来查看用户的更新余额。新余额应为：L1 余额 – ```400``` 和 L2 余额 – ```2933。```

### 估算消息费用

您可以在发送之前从 L1 估算给定消息的 L2 成本。 ```estimate_message_fee``` 命令估计在不影响合约状态的情况下处理来自 L1 的消息的 L2 成本。 这类似于对调用函数使用 ```estimate_fee``` 标志。 结果以 WEI 和 ETH 表示，如下所示。

首先，设置您要模拟的消息的发件人地址（L1 地址）。 在我们的例子中，使用上一节中示例合约的地址。

```
export L1_CONTRACT_ADDRESS=0x8359E4B0152ed5A731162D3c7B0D8D56edB165A0
```

现在，要估算给定消息的费用（在本例中 – 调用 deposit()），请运行以下命令：

```
starknet estimate_message_fee \
    --from_address ${L1_CONTRACT_ADDRESS} \
    --address ${CONTRACT_ADDRESS} \
    --abi l1l2_abi.json \
    --function deposit \
    --inputs \
        ${USERID} \
        200
```

输出应类似于：

```
The estimated fee is: 2081500000000000 WEI (0.002082 ETH).
Gas usage: 20815
Gas price: 100000000000 WEI
```